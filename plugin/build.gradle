plugins {
    id 'groovy'
    id 'java'
    id 'maven-publish'
    id "com.github.gmazzo.buildconfig" version "1.6.1"
}

group 'com.ahasbini.tools'
version '0.0.4-dev'

sourceCompatibility = 1.8

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    //noinspection GroovyAssignabilityCheck
    inputs.files sourceSets.main.runtimeClasspath.filter { File f ->
        // Exclude gradle & groovy jars only as they are not need in injection
        !f.name.matches("gradle-api-.*\\.jar") &&
                !f.name.matches("groovy-all-.*\\.jar") &&
                !f.name.matches("gradle-installation-beacon-.*\\.jar") &&
                !f.name.matches("slf4j-simple-.*\\.jar") &&
                !f.name.matches("slf4j-api-.*\\.jar")

    }
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.filter { File f ->
            // Exclude gradle & groovy jars only as they are not need in injection
            !f.name.matches("gradle-api-.*\\.jar") &&
                    !f.name.matches("groovy-all-.*\\.jar") &&
                    !f.name.matches("gradle-installation-beacon-.*\\.jar") &&
                    !f.name.matches("slf4j-simple-.*\\.jar") &&
                    !f.name.matches("slf4j-api-.*\\.jar")
        }.join("\n")
    }
}

sourceSets {
    //noinspection GroovyAssignabilityCheck
    main {
        groovy {
            srcDirs = [ srcDirs, sourceSets.main.java.srcDirs.flatten()]
        }
        java {
            srcDirs = [] // don't compile Java code twice
        }
    }
}

def toolingApiVersion = gradle.gradleVersion

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'de.undercouch:gradle-download-task:4.0.0'
    implementation "org.gradle:gradle-tooling-api:${toolingApiVersion}"

    testImplementation gradleTestKit()
    testImplementation 'junit:junit:4.12'

    //noinspection GroovyAssignabilityCheck
    testRuntimeOnly files(createClasspathManifest)
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'android-opencv-gradle-plugin'

            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

buildConfig {
    packageName group + ".androidopencv"

    buildConfigField("String", "APP_NAME", "\"AndroidOpenCVGradlePlugin\"")
    buildConfigField("String", "APP_VERSION", "\"${project.version}\"")
}

task sourceSets {
    doLast {
        sourceSets.forEach() { SourceSet s ->
            println "$s.name"
            println "-------------------------"
            println("java: " + s.java.sourceDirectories.files)
            println("groovy: " + s.groovy.sourceDirectories.files)
            println ""
        }
    }
}